import es.unizar.disco.pnml.utils.PnmlDiceUtils;
import helpers;

modeltype UML uses 'http://www.eclipse.org/uml2/5.0.0/UML';
modeltype PNML uses 'http:///ptnet.ecore';
modeltype DEF uses 'http://es.unizar.disco/simulation/definition/1.0';
modeltype INVOC uses 'http://es.unizar.disco/simulation/invocation/1.0';
modeltype CONST uses 'http://es.unizar.disco/pnconstants/1.0';
modeltype ECORE uses 'http://www.eclipse.org/emf/2002/Ecore';


transformation uml2pnml(in invocation : INVOC, out res : PNML) 
access helpers;

main() {
	invocation.activeScenario() -> map model2net();
}

mapping UML::NamedElement::model2net() : PNML::PetriNetDoc {
	nets := object PNML::PetriNet {
		id := 'net';
		name := createName(self.name);
		pages := object PNML::Page {
			id := 'page';
		}
	}
}

abstract mapping UML::NamedElement::namedElement2node(in id : String) : PNML::PnObject
{
	result.containerPage := getGlobalPage();
	result.id := id;
	if (result.oclIsKindOf(PNML::Node) and name.oclIsUndefined()) {
		name := createName(id);
	};	
} 

mapping UML::NamedElement::namedElement2place(in id : String) : PNML::Place 
inherits UML::NamedElement::namedElement2node
{
	init {
		result := resolveoneIn(UML::NamedElement::namedElement2place, pnObject : PNML::Place | pnObject.id = id);
	}
	/*
	var pattern := self.getPatternVsl();
	if (pattern != null) {
		var value := pattern.getPopulationValue(invocation.varAssignments());
		if (value != null) {
			initialMarking := object PNML::PTMarking {
				text := value.round();
			}
		}
	};
	*/
} 

mapping UML::NamedElement::namedElement2transition(in id : String) : PNML::Transition 
inherits UML::NamedElement::namedElement2node
{
	init {
		result := resolveoneIn(UML::NamedElement::namedElement2transition, pnObject : PNML::Transition | pnObject.id = id);
	}
	/*
	var hostDemand := self.getHostDemandVsl();
	if (hostDemand->notEmpty()) {
		hostDemand->forEach(elt) {
			-- hostDemand is many-valued, so that, we search for the value in all the entries
			-- only the first valid value will be used
			var value := elt.getValueValue(invocation.varAssignments());
			if (value != null) {
				result.setTransitionKind(CONST::TransitionKind::Exponential, (1 / value))
			}
		}
	};
	var probability := self.getProbabilityVsl();
	if (probability != null) {
		var value := probability.getValueValue(invocation.varAssignments());
		if (value != null) {
			result.setTransitionKind(CONST::TransitionKind::Immediate, value)
		}
	};
	*/
} 

mapping UML::NamedElement::namedElement2arc(in source : PNML::Node, in target : PNML::Node) : PNML::Arc 
{
	init {
		var id := source.id + '_to_' + target.id;
		result := resolveoneIn(UML::NamedElement::namedElement2arc, pnObject : PNML::Arc | pnObject.id = id);
	}
	result.id := id;
	result.containerPage := getGlobalPage();
	result.source := source;
	result.target:= target;
} 

helper getGlobalPage() : PNML::Page
{
	return resolveoneIn(UML::NamedElement::model2net, PNML::PetriNetDoc).nets.pages->asOrderedSet()->first();
}

helper INVOC::currentInvocation() : INVOC::SimulationInvocation {
	return self.objectsOfType(INVOC::SimulationInvocation)->asSequence()->first();
}

helper INVOC::activeScenario() : UML::Behavior {
	return self.currentInvocation().definition.activeScenario.oclAsType(UML::Behavior);
}
/********************************************************
  Utility intermediate classes for NFP Types
********************************************************/

helper String::key() : String {
	if (self.indexOf("=") = -1) {
		abort("Unexpected number of tokens in " + self);
	} else {
		return self.substringBefore("=")
	};
	return null;
}

helper String::value() : String {
	if (self.indexOf("=") = -1) {
		abort("Unexpected number of tokens in " + self);
	} else {
		return self.substringBefore("=")
	};	
	return null;
}

helper String::getNfpValue(key : String) : String {
	var entries := self.tokenize(",");
	entries->forEach(entry) {
		if (entry.key() = key) {
			return entry.value();
		}
	};
	return null;
}